! The main program to model a tsunami (Simplified)

program tsunami

    use mod_diff, only: diff ! using the mod_diff module from the src dir

    implicit none

    ! Data Declaration
    integer :: i,n

    integer, parameter :: grid_size = 100, num_time_steps = 100
    integer, parameter :: icenter = 50
    real, parameter :: decay = 0.02, u = 1.0, dx = 1.0, dt = 1.0
    real, DIMENSION(grid_size) :: h

    ! loop over array elements and initialise values
    init : do i = 1,grid_size
        h(i) = exp(-decay * (i-icenter)**2)
    end do init

    n = 0

    ! write initial water height values to the terminal
    print*, n, h

    time_loop : do n = 1,num_time_steps

        ! no need to mention array slices below explicitly 
        ! as it is from 1 to grid_size
        h = h - u * diff(h) / dx * dt 

        print*, n, h
    end do time_loop

end program tsunami
